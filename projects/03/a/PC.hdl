// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=previousValue, out=incValue);
    Mux16(a=previousValue, b=incValue, sel=inc, out=toMux1);
    Mux16(a=toMux1, b=in, sel=load, out=toMux2);
    Mux16(a=toMux2, b=false, sel=reset, out=toRegister);
    Register(in=toRegister, load=true, out=previousValue, out=out);
}
